<html>
<head></head>
<body>
<pre>
; Sa se citeasca de la tastatura numele unui fisier. Sa se afiseze acest fisier pe ecran.

assume cs:code, ds:data
data segment
	msg db 'Numele fisierului: $'
	maxFileName db 12
	lFileName db ?
	fileName db 12 dup (?)
	buffer db 100 dup (?), '$'
	openErrorMsg db 'Fisierul nu exista.$'
	readErrorMsg db 'Nu se poate citii din fisier.$'
data ends
code segment
start:
	mov ax, data
	mov ds, ax

	; afisam mesajul cu functia 09h, int 21h
	mov ah, 09h
	mov dx, offset msg
	int 21h

	; citim de la tastatura numele fisierului cu ajutorul functiei 0ah, int 21h
	mov ah, 0ah
	mov dx, offset maxFileName
	int 21h
	; in urma citirii la adresa maxFileName + 2 = fileName se memoreaza numele fisierului citit
	; la adresa maxFileName + 1 = lFileName se memoreaza dimensiunea sirului de caractere care reprezinta numele fisierului
 
	; transformam numele fisierului intr-un sir ASCIIZ (sir ce se termina cu byte-ul zero)
	mov al, lFileName
	xor ah, ah
	mov si, ax
	mov fileName[si], 0

	; deschidem fisierul cu functia 3dh, int 21h
	mov ah, 3dh
	mov al, 0 ; deschidem fisierul pentru citire
	mov dx, offset fileName
	int 21h

	jc openError ; eroare la deschiderea fisierului daca CF e setat
	mov bx, ax ; salvam identificatorul fisierului in registrul bx
 
	; cat timp nu e sfarsit de fisier citim din fisier si afisam pe ecran
	goOn:
		mov ah, 3fh
		mov dx, offset buffer
		mov cx, 100 ; citim maxim 100 de caractere
		int 21h
		jc readError	; daca va avea loc o eroare, procesorul seteaza CF=1

		; in caz de succes functia 3dh intoarce in ax numarul de octeti cititi
		; salvam numarul octetilor cititi si pregatim sirul pentru afisare
		mov si, ax
		mov buffer[si], '$'

		; afisam ce am citit cu functia 09h a intreruperii 21h
		mov ah, 09h
		int 21h
		cmp si, 100
		je goOn ; daca am citit 100 de octeti inseamna ca nu am terminat de citit

 
	jmp endPrg ; sarim peste tratarea eventualelor erori

	openError:
		mov ah, 09h
		mov dx, offset openErrorMsg
		int 21h
		jmp endPrg 

	readError:
		mov ah, 09h
		mov dx, offset readErrorMsg
		int 21h 

	endPrg:
		mov ax,4c00h
		int 21h
code ends
end start
</pre>
</body>
</html>
